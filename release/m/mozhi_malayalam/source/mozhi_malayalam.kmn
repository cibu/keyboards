store(&VERSION) '9.0'
c Malayalam Keyman Keyboard for Mozhi Keyboard Layout
c Original work done by Raj Nair, but the source is not available
c This keyboard was created by Junaid P. V.
c This keyboard closely follows Mozhi Spec (https://sites.google.com/site/cibu/mozhi/mozhi2)
store(&NAME) 'Malayalam Mozhi'
store(&KEYBOARDVERSION) '3.0.1.8' 
store(&BITMAP) 'mozhi_malayalam.ico'
store(&COPYRIGHT) '© 2021-2022 Cibu C. J.; Benjamin C. Varghese'
store(&TARGETS) 'windows macosx linux web'
store(&VISUALKEYBOARD) 'mozhi_malayalam.kvks'
store(&MESSAGE) 'Malayalam Keyboard for Mozhi 2.1'
store(&ETHNOLOGUECODE) 'mal'
store(&LANGUAGE) 'x044C'
store(&WINDOWSLANGUAGES) 'x044C'
begin Unicode > use(main)


store(chillu) 'ൺൻർൽൾൿ'
store(chillu_bases) 'ണനരലളക'

store(unaspirated) 'കഗജടഡതദപബ'
store(aspirated)   'ഖഘഝഠഢഥധഫഭ'

store(eng) 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ'

store(mala)  'ഀഁംഃഄ'\
             'അആഇഈഉഊഋഌ഍എഏഐ഑ഒഓഔ'\
             'കഖഗഘങ'\
             'ചഛജഝഞ'\
             'ടഠഡഢണ'\
             'തഥദധനഩ'\
             'പഫബഭമ'\
             'യരറലളഴവ'\
             'ശഷസഹഺ'\
             '഻഼ഽ'\
             'ാിീുൂൃൄ൅െേൈ൉ൊോൌ്ൎ'\
             '൏'\
             '൐൑൒൓'\
             'ൔൕൖ'\
             'ൗ'\
             '൘൙൚൛൜൝൞ൟൠൡൢൣ'\
             '൤൥'\
             '൦൧൨൩൪൫൬൭൮൯൰൱൲൳൴൵൶൷൸'\
             '൹'\
             'ൺൻർൽൾൿ'

store(anu2au)  'ഀഁംഃഄ'\
             'അആഇഈഉഊഋഌ഍എഏഐ഑ഒഓഔ'\
             'കഖഗഘങ'\
             'ചഛജഝഞ'\
             'ടഠഡഢണ'\
             'തഥദധനഩ'\
             'പഫബഭമ'\
             'യരറലളഴവ'\
             'ശഷസഹഺ'\
             '഻഼ഽ'\
             'ാിീുൂൃൄ൅െേൈ൉ൊോൌ'\


store(mala_sans_virama)  'ഀഁംഃഄ'\
             'അആഇഈഉഊഋഌ഍എഏഐ഑ഒഓഔ'\
             'കഖഗഘങ'\
             'ചഛജഝഞ'\
             'ടഠഡഢണ'\
             'തഥദധനഩ'\
             'പഫബഭമ'\
             'യരറലളഴവ'\
             'ശഷസഹഺ'\
             '഻഼ഽ'\
             'ാിീുൂൃൄ൅െേൈ൉ൊോൌൎ'\
             '൏'\
             '൐൑൒൓'\
             'ൔൕൖ'\
             'ൗ'\
             '൘൙൚൛൜൝൞ൟൠൡൢൣ'\
             '൤൥'\
             '൦൧൨൩൪൫൬൭൮൯൰൱൲൳൴൵൶൷൸'\
             '൹'\
             'ൺൻർൽൾൿ'

store(mala_ext_digits) '൦൧൨൩൪൫൬൭൮൯൰൱൲൳൴൵൶൷൸'

store(ka2ta) 'കഖഗഘങ'\
             'ചഛജഝഞ'\
             'ടഠഡഢണ'\
             'തഥദധനഩ'\
             'പഫബഭമ'\
             'യരറലളഴവ'\
             'ശഷസഹഺ'\


store(la_conjuncts)  'കഗതപഫബമലവശസഹ' c Consonants with la forms a conjunct form
store(nna_conjuncts) 'ണഷണ'
store(na_conjuncts)  'ഗഘതസധനപമശസഹഴ'
store(ma_conjuncts)  'ഗണതസനമശസഹ'
store(ha_bases)      'കഗചജടഡതദപബസശ'
store(virama_keys) '`~'
store(mozhi_breakers) '_-'
store(digits) '0123456789'
store(mala_digits) '൦൧൨൩൪൫൬൭൮൯'

store(breakers) '0'
store(isEng) '0'

group(main) using keys

+ 'a' > use(lower_a)
+ 'A' > use(upper_a)
+ 'b' > use(lower_b)
+ 'B' > use(upper_b)
+ 'c' > use(lower_c)
+ 'C' > use(upper_c)
+ 'd' > use(lower_d)
+ 'D' > use(upper_d)
+ 'e' > use(lower_e)
+ 'E' > use(upper_e)
+ 'f' > use(lower_f)
+ 'F' > use(upper_f)
+ 'g' > use(lower_g)
+ 'G' > use(upper_g)
+ 'h' > use(lower_h)
+ 'H' > use(upper_H)
+ 'i' > use(lower_i)
+ 'I' > use(upper_i)
+ 'j' > use(lower_j)
+ 'J' > use(upper_j)
+ 'k' > use(lower_k)
+ 'K' > use(upper_k)
+ 'l' > use(lower_l)
+ 'L' > use(upper_l)
+ 'm' > use(lower_m)
+ 'M' > use(upper_m)
+ 'n' > use(lower_n)
+ 'N' > use(upper_n)
+ 'o' > use(lower_o)
+ 'O' > use(upper_o)
+ 'p' > use(lower_p)
+ 'P' > use(upper_p)
+ 'q' > use(lower_q)
+ 'Q' > use(upper_q)
+ 'r' > use(lower_r)
+ 'R' > use(upper_r)
+ 's' > use(lower_s)
+ 'S' > use(upper_s)
+ 't' > use(lower_t)
+ 'T' > use(upper_t)
+ 'u' > use(lower_u)
+ 'U' > use(upper_u)
+ 'v' > use(lower_v)
+ 'V' > use(upper_v)
+ 'w' > use(lower_w)
+ 'W' > use(upper_w)
+ 'x' > use(lower_x)
+ 'X' > use(upper_x)
+ 'y' > use(lower_y)
+ 'Y' > use(upper_y)
+ 'z' > use(lower_z)
+ 'Z' > use(upper_z)
+ '^' > use(caret)
+ '#' > use(hash)
+ '\' > use(back_slash)
+ '$' > U+20B9 c Indian Rupee Sign
+ any(virama_keys) > use(virama)

+ any(mozhi_breakers) > index(mozhi_breakers, 1) dk(mozhi_breakers)
any(mozhi_breakers) dk(mozhi_breakers) + '$' > '$'
any(mala_ext_digits) + any(digits) > context index(mala_digits, 2)

if(isEng='1') + any(eng) > index(eng, 2) set(isEng='0')
any(eng) + any(eng) > context index(eng, 2)

c Emoticons
':' + 'O' > '😳'
'|' + 'O' > '🤦'


group(lower_a) using keys
c ===========================
c handles the keypress 'a'
c --------------------------- 
+ 'a' > 'അ' 

'ാ' + 'a' > context 'ാ'
'്' + 'a' > nul 
if(vir='1') '്' + 'a' > '്അ' set(vir='0')
'ച്' dk(h) + 'a' > 'ച'
'ം' + 'a' > 'മ'
if(vir='1') 'മ്' + 'a' > 'മ്അ' set(vir='0')
'അ' + 'a' > 'ആ'
'്അ' + 'a' > 'ാ'
'ഇ' + 'a' > 'ഈ'
'ഉ' + 'a' > 'ഊ'
'ഋ' + 'a' > 'റ'
'എ' + 'a' > 'ഏ'
'ഒ' + 'a' > 'ഓ'
'ി' + 'a' > 'ീ'
'ു' + 'a' > 'ൂ'
'ൃ' + 'a' > '്ര'
'െ' + 'a' > 'േ'
'ൊ' + 'a' > 'ോ'
'ഋ' + 'a' > 'റ'
'ൃ' + 'a' > 'റ'

'യൃ' + 'a' > 'യ്‌ര'
'്ട്ര്' + 'a' > '്ട്ര'
'ൻട്ര്' + 'a' > 'ൻട്ര'
'റ്രറ്' + 'a' > 'റ്രറ'

any(chillu) + 'a' > index(chillu_bases,1)
any(mozhi_breakers) dk(mozhi_breakers) + 'a' > 'അ'
any(ka2ta) + 'a' > index(ka2ta,1) U+0D3E

'ള്‌ൾ' + 'a' > 'ള്' U+200C 'ള'
'്ർ' + 'a' > '്' U+200C 'ര'
if(vir='1') any(la_conjuncts) '്ൽ' + 'a' > index(la_conjuncts, 2) '്' U+200C 'ല' set(vir='0')
if(vir='1') any(nna_conjuncts) '്ൺ' + 'a' > index(nna_conjuncts, 2) '്' U+200C 'ണ' set(vir='0')
if(vir='1') any(na_conjuncts) '്ൻ' + 'a' > index(na_conjuncts, 2) '്' U+200C 'ന' set(vir='0')
if(vir='1') any(ma_conjuncts) '്ം' + 'a' > index(ma_conjuncts, 2) '്' U+200C 'മ' set(vir='0')

any(ma_bases) '്ം' any(mozhi_breakers) dk(mozhi_breakers) + 'a' > index(ma_bases, 1) '്' U+200C 'മ' 

group(upper_a) using keys
c ===========================
c handles the keypress 'A'
c --------------------------- 
+ 'A' > 'ആ'
'ആ' + 'A' > 'ആാ'
'ാ' + 'A' > 'ാാ'
'ഋ' + 'A' > 'റാ'
'ൃ' + 'A' > '്രാ'
'്' + 'A' > 'ാ'

'ച്' dk(h) + 'A' > 'ചാ'
'യൃ' + 'A' > 'യ്' U+200C 'രാ'
'യ്ര്' + 'A' > 'യ്ര്രാ'
'ൃർ' + 'A' > '്റാ'
'റ്' + 'A' > 'റാ'
'്ർ' + 'A' > '്രാ'

'ഋ' + 'A' > 'റാ'
'ൃ' + 'A' > 'റാ'

any(chillu) + 'A' > index(chillu_bases,1) 'ാ'
'ം' + 'A' > 'മാ'

group(lower_b) using keys
c ===========================
c handles the keypress 'b'
c --------------------------- 
+ 'b' > 'ബ്'

group(upper_b) using keys
c ===========================
c handles the keypress 'B'
c --------------------------- 
+ 'B' > 'ബ്ബ്'

group(lower_c) using keys
c ===========================
c handles the keypress 'c'
c --------------------------- 
'ൻ' + 'c' > 'ഞ്ച്'
+ 'c' > 'ച്'

group(upper_c) using keys
c ===========================
c handles the keypress 'C'
c --------------------------- 
+ 'C' > 'ച്ച്'

group(lower_d) using keys
c ===========================
c handles the keypress 'd'
c --------------------------- 
+ 'd' > 'ദ്'
'ഡ്' + 'd' > 'ഡ്ഡ്'
'ൺ' + 'd' > 'ണ്ട്'
'ണ്‌ദ്' + 'd' > 'ൺദ്ദ്'
'ദ്' + 'd' > 'ദ്ദ്'
'ദ്ദ്' + 'd' > 'ഡ്'
'ൻ' + 'd' > 'ന്ദ്'
'ന്ദ്' + 'd' > 'ൻദ്ദ്'

any(mozhi_breakers) dk(mozhi_breakers) + 'd' > 'ദ്'

group(upper_d) using keys
c ===========================
c handles the keypress 'D'
c --------------------------- 
+ 'D' > 'ഡ്'
'ക്ഷ്' + 'D' > '😆'
'ൺ' + 'D' > 'ണ്ഡ്'
'ണ്ഡ്' + 'D' > 'ൺഡ്ഡ്'

'ൺ' any(mozhi_breakers) dk(mozhi_breakers) + 'D' > 'ൺഡ്'

group(lower_e) using keys
c ===========================
c handles the keypress 'e'
c --------------------------- 
+ 'e' > 'എ'
'ഈ' + 'e' > context 'ൗ'
'ീ' + 'e' > context 'ീ'
'ൗ' + 'e' > context 'ൗ'
'അ' + 'e' > 'ഏ'
'ഇ' + 'e' > 'ഈ'
'ഋ' + 'e' > 'റെ'
'എ' + 'e' > 'ഈ'
'ഒ' + 'e' > 'ഓ'
'ം' + 'e' > 'മെ'
'്' + 'e' > 'െ' set(h='0')
'ി' + 'e' > 'ീ'
'ൃ' + 'e' > '്രെ'
'െ' + 'e' > 'ീ'
'ൊ' + 'e' > 'ോ'
'ച്' dk(h) + 'e' > 'ചെ'

'ൻററ്' + 'e' > 'ൻെററ'
'ൻ്രറ്യ്' + 'e' > 'ൻ്രെറ്യ'
'റ്രറ്യ്' + 'e' > 'റ്രെറ്യ'
'ൻറ്' + 'e' > 'ൻെറ'
'ററ്' + 'e' > 'റെറ'
any(ka2ta) + 'e' > context 'േ'

'ൻ്രറ്' + 'e' > 'ൻ്രെറ'
'ൻട്ര്' + 'e' > 'ൻട്രെ'
'ഋ' + 'e' > 'റെ'
'റ്രറ്' + 'e' > 'റ്രെറ'
if(r='1') 'ൃർ' + 'e' > '്റെ' set(r='0')
'ൃ' + 'e' > '്രെ'


'ള്‌ൾ' + 'e' > 'ള്' U+200C 'ളെ'
'്ർ' + 'e' > '്' U+200C 'രെ'
any(la_conjuncts) '്ൽ' + 'e' > index(la_conjuncts, 1) '്' U+200C 'ലെ'
any(nna_conjuncts) '്ൺ' + 'e' > index(nna_conjuncts, 1) '്' U+200C 'ണെ'
any(na_conjuncts) '്ൻ' + 'e' > index(na_conjuncts, 1) '്' U+200C 'നെ'
any(ma_conjuncts) '്ം' + 'e' > index(ma_conjuncts, 1) '്' U+200C 'മെ'

any(mala) any(mozhi_breakers) dk(mozhi_breakers) + 'e' > index(mala, 1) 'എ'

any(chillu) + 'e' > index(chillu_bases, 1) 'െ'

group(upper_e) using keys
c ===========================
c handles the keypress 'E'
c --------------------------- 
+ 'E' > 'ഏ'

'്' + 'E' > 'േ'
'ഏ' + 'E' > 'ഏേ'
'േ' + 'E' > 'േേ'
'ഋ' + 'E' > 'റേ'
'ൃ' + 'E' > '്രേ'
'ൃ' dk(r)  + 'E' > '്' U+200C 'രേ'
'ച്' dk(h) + 'E' > 'ചേ'

'ൻറ്' + 'E' > 'ൻേറ'

'ൻററ്' + 'E' > 'ൻേററ'
'ൻ്രററ' + 'E' > 'ൻ്രേററ'
'ൻ്രററ്യ്' + 'E' > 'ൻ്രേററ്യ'
'ൻ്രറ്യ്' + 'E' > 'ൻ്രേറ്യ'
'റ്രറ്യ്' + 'E' > 'റ്രേറ്യ'

'ൻറ്' + 'E' > 'ൻേറ'
'ററ്' + 'E' > 'റേറ'

'ള്‌ൾ' + 'E' > 'ള്' U+200C 'ളേ'
'്ർ' + 'E' > '്' U+200C 'രേ'
'യൃ' + 'E' > 'യ്' U+200C 'രേ'

any(la_conjuncts) '്ൽ' + 'E' > index(la_conjuncts, 1) '്' U+200C 'ലേ'
any(nna_conjuncts) '്ൺ' + 'E' > index(nna_conjuncts, 1) '്' U+200C 'ണേ'
any(na_conjuncts) '്ൻ' + 'E' > index(na_conjuncts, 1) '്' U+200C 'നേ'
any(ma_conjuncts) '്ം' + 'E' > index(ma_conjuncts, 1) '്' U+200C 'മേ'

any(chillu) + 'E' > index(chillu_bases, 1) 'േ'
'ം' + 'E' > 'മേ'

group(lower_f) using keys
c ===========================
c handles the keypress 'f'
c --------------------------- 
+ 'f' > 'ഫ്'

group(upper_f) using keys
c ===========================
c handles the keypress 'F'
c --------------------------- 
+ 'F' > 'ഭ്'

group(lower_g) using keys
c ===========================
c handles the keypress 'g'
c --------------------------- 
+ 'g' > 'ഗ്'

'ങ്ങ്' + 'g' > 'ങ്ഗ്'
dk(n) + 'g' > nul
'ന്' + 'g' > 'ങ്'
'ൻ' + 'g' > 'ങ്'
'ർ' + 'g' > 'ഋഗ്'
'റ്' + 'g' > 'ഋഗ്'

any(mala) 'ൻ' + 'g' > index(mala, 1) 'ങ്ങ്'
any(mala) 'ർ' + 'g' > context 'ഗ്'

'ൻ' any(mozhi_breakers) dk(mozhi_breakers) + 'g' > 'ൻഗ്'
any(mala) any(mozhi_breakers) dk(mozhi_breakers) 'ൻ' + 'g' > index(mala, 1) 'ങ്' 

group(upper_g) using keys
c ===========================
c handles the keypress 'G'
c --------------------------- 
+ 'G' > 'ഗ്ഗ്'
'ൺ' + 'G' > 'ങ്ങ്'

group(lower_h) using keys
c ===========================
c handles the keypress 'h'
c --------------------------- 
store(h) '0'

+ 'h' > 'ഹ്'
'ഞ്ച്' + 'h' > 'ഞ്ച്'
'ന്ച്' + 'h' > 'ഞ്ച്'  
'ഞ്ഞ്ച്' + 'h' > 'ഞ്ച്' 
'ച്' + 'h' > 'ച്' dk(h)
'ച്' dk(h) + 'h' > 'ഛ്' 
c 'ച്' dk(h) + 'h' > 'ഛ്' c set(h='0')

c 'ഋറ്റ്' + 'h' > 'ത്' c TODO
'ത്ത്' + 'h' > 'ത്ത്' dk(h)
'ത്ത്' dk(h) + 'h' > 'ത്ഥ്'
'റ്റ്' + 'h' > 'ത്'
'ഡ്' + 'h' > 'ഢ്'
'ൻ്റ്' + 'h' > 'ന്ത്'
'ണ്ട്' + 'h' > 'ണ്ഠ്'
'ഋറ്റ്' + 'h' > 'ഋത്'
'ട്ട്' + 'h' > 'ഠ്'

any(mala) 'ട്' + 'h'  > index(mala, 1) 'ഠ്'
any(mala) 'റ്റ്' + 'h'  > index(mala, 1) 'ത്'
any(mala) 'ർട്' + 'h' > index(mala, 1) 'ർത്'
'ഋ' 'റ്റ്' + 'h' > 'ഋത്'
any(unaspirated) '്' + 'h' > index(aspirated, 1) '്'
'ൺൻ' + 'h' > 'ണ്ണ്'
'ൻ' + 'h' > 'ൺ'
'ന്' + 'h' > 'ൺ'
'ന്ന്' + 'h' > 'ണ്ണ്'
'ന്‌റ്' + 'h' > 'ന്ത്'
'ല്' + 'h' > 'ള്'
'ൽ' + 'h' > 'ൾ'
'ല്ല്' + 'h' > 'ള്ള്'
'ശ്' + 'h' > 'ഴ്'
'സ്' + 'h' > 'ഷ്'
'ഹ്' + 'h' > 'ഃ'
'ത്ത്' dk(thth) + 'h' > 'ത്ഥ്'

'ട്' + 'h' > 'ത്'
'ത്ത്' + 'h' > 'ത്ത്' dk(thth)
'റ്റ്' + 'h' > 'ഠ്'
'ഡ്' + 'h' > 'ഢ്'
'ൻ്റ്' + 'h' > 'ന്ത്'
'ണ്ട്' + 'h' > 'ണ്ഠ്'
'ഋറ്റ്' + 'h' > 'ഋത്'
'ഋറ്റ്' + 'h' > 'ഋത്'
'ട്ട്' + 'h' > 'ഠ്'

any(mala) 'ർസ്' + 'h' > index(mala, 1) 'ർഷ്'
'ർസ്' + 'h' > 'ഋഷ്'
'റ്സ്' + 'h' > 'ഋഷ്'
'റ്റ്' + 'h' > 'റ്റ്ഷ്'

any(mala) 'ർറ്റ്' + 'h' > index(mala, 1) 'ർത്'
any(ha_bases) '്' any(mozhi_breakers) dk(mozhi_breakers) + 'h' > index(ha_bases, 1) '്ഹ്'
'ൻ' any(mozhi_breakers) dk(mozhi_breakers) + 'h' > 'ൻഹ്'
'ൽ' any(mozhi_breakers) dk(mozhi_breakers) + 'h' > 'ൽഹ്'

group(upper_h) using keys
c ===========================
c handles the keypress 'H'
c --------------------------- 
+ 'H' > 'ഃ'
'ച്ച്' + 'H' > context dk(h)
'ട്' + 'H' > 'ത്ത്'

group(lower_i) using keys
c ===========================
c handles the keypress 'i'
c --------------------------- 
+ 'i' > 'ഇ'

'ഈ' + 'i' > 'ഈൗ'
'ള്‌ൾ' + 'i' > 'ള്' U+200C 'ളി'
'ീ' + 'i' > 'ീീ'
'ൗ' + 'i' > 'ൗൗ'
'്ർ' + 'i' > '്' U+200C 'രി'
'അ' + 'i' > 'ഐ'
'ഇ' + 'i' > 'ഈ'
'ഋ' + 'i' > 'റി'
'എ' + 'i' > 'ഐ'
'ി' + 'i' > 'ീ'
'ൃ' + 'i' > '്രി'
'ം' + 'i' > 'മി'
'െ' + 'i' > 'ൈ'
'്' + 'i' > 'ി'
'ച്' dk(h) + 'i' > 'ചി'


any(chillu) + 'i' > index(chillu_bases,1) 'ി'
any(ka2ta) '്' + 'i' > index(ka2ta, 1) 'ി'
any(ka2ta) + 'i' > index(ka2ta, 1) 'ൈ'

any(la_conjuncts) '്ൽ' dk(non_joiner) + 'i' > index(la_conjuncts, 1) '്' U+200C 'ലി'
any(nna_conjuncts) '്ൺ' dk(non_joiner) + 'i' > index(nna_conjuncts, 1) '്' U+200C 'ണി'
any(na_conjuncts) '്ൻ' dk(non_joiner) + 'i' > index(na_conjuncts, 1) '്' U+200C 'നി'
any(ma_conjuncts) '്ം' dk(non_joiner) + 'i' > index(ma_conjuncts, 1) '്' U+200C 'മി'

any(mala) any(mozhi_breakers) dk(mozhi_breakers) + 'i' > index(mala, 1) 'ഇ'

group(upper_i) using keys
c ===========================
c handles the keypress 'I'
c --------------------------- 
+ 'I' > 'ഈ'

'ീ' + 'I' > 'ീീ'
'ം' + 'I' > 'മീ'
'ഋ' + 'I' > 'റീ'
'ൃ' + 'I' > '്രീ'
'്' + 'I' > 'ീ'
'ഈ' + 'I' > 'ഈൗ'
'ൗ' + 'I' > 'ൗൗ'
'ൃ' dk(r)  + 'I' > '്' U+200C 'രീ'
'ച്' dk(h) + 'I' > 'ചീ'


any(chillu) + 'I' > index(chillu_bases,1) 'ീ'

group(lower_j) using keys
c ===========================
c handles the keypress 'j'
c --------------------------- 
+ 'j' > 'ജ്'

'ന്' + 'j' > 'ഞ്'
'ൻ' + 'j' > 'ഞ്'
'ർ' + 'j' > 'ഋജ്'
'റ്' + 'j' > 'ഋജ്'
'റ്റ്' + 'j' > 'റ്റ്ജ്'
'ഞ്ഞ്' + 'j' > context
any(mala_sans_virama) 'ൻ' + 'j' > index(mala_sans_virama, 1) 'ഞ്ഞ്'
any(mala) 'ർ' + 'j'  > index(mala, 1) 'ർജ്'

'ൻ' any(mozhi_breakers) dk(mozhi_breakers) + 'j' > 'ൻജ്'
any(mala) any(mozhi_breakers) dk(mozhi_breakers) 'ൻ' + 'j' > index(mala, 1) 'ഞ്' 

group(upper_j) using keys
c ===========================
c handles the keypress 'J'
c --------------------------- 
+ 'J' > 'ജ്ജ്'

'ൺ' + 'J' > 'ഞ്ഞ്'


group(lower_k) using keys
c ===========================
c handles the keypress 'k'
c --------------------------- 
+ 'k' > 'ക്'
'ൻ' + 'k' > 'ങ്ക്'
'ർ' + 'k' > 'ഋക്'
'റ്റ്' + 'k' > 'റ്റ്ക്'

any(mala) 'ർ'	+ 'k' > context 'ക്'
'ൻ' any(mozhi_breakers) dk(mozhi_breakers) + 'k' > 'ൻക്'

group(upper_k) using keys
c ===========================
c handles the keypress 'K'
c --------------------------- 
+ 'K' > 'ക്ക്'

group(lower_l) using keys
c ===========================
c handles the keypress 'l'
c --------------------------- 
store(la_bases) 'കഗതപഫബമലവശസഹ'

+ 'l' > 'ൽ'

'ം' + 'l' > 'മ്ല്'
'ഌ' + 'l' > 'ൡ'
'മ്ല്' + 'l' > 'ംല്ല്'
'ൽ' + 'l' > 'ല്ല്'
'ല്ല്' + 'l' > 'ൽല്ല്'
'ൾ' + 'l' > 'ള്ള്'
'ൢ' + 'l' > 'ൣ'

any(la_conjuncts) '്' + 'l' > context 'ല്'
any(la_bases)  '്' any(mozhi_breakers) dk(mozhi_breakers) + 'l' > index(la_bases, 1) '്ൽ'

group(upper_l) using keys
c ===========================
c handles the keypress 'L'
c --------------------------- 
store(lLm) 'ൽൾം'

+ 'L' > 'ൾ'
'ം' + 'L' > 'മ്ള്'
'ൾ' + 'L' > 'ള്ള്'
any(lLm) any(mozhi_breakers) dk(mozhi_breakers) + 'L' > index(lLm, 1) 'ൾ' 
'ള്' any(mozhi_breakers) dk(mozhi_breakers) + 'L' > 'ള്ൾ' 

group(lower_m) using keys
c ===========================
c handles the keypress 'm'
c --------------------------- 
store(ma_bases)   'ഗണതസനമശസഹ'
store(ma_chillus) 'ൻൺൽം'

+ 'm' > 'ം'

'ംം' + 'm' > context 'ം'
'ങ്ങ്' + 'm' > 'ങ്മ്'
'ം' + 'm' > 'മ്മ്'
'ൺ' + 'm' > 'ണ്മ്'
'ണ്മ്' + 'm' > 'ൺമ്മ്'
'ൻ' + 'm' > 'ന്മ്'
'ന്മ്' + 'm' > 'ൻമ്മ്'
'മ്മ്' + 'm' > 'ംംം'
dk(h) + 'm' > 'ം'

any(ma_bases) '്' any(mozhi_breakers) dk(mozhi_breakers) + 'm' > index(ma_bases, 1) '്ം' 
any(ma_chillus) any(mozhi_breakers) dk(mozhi_breakers) + 'm' > index(ma_chillus, 1) 'ം' 

group(upper_m) using keys
c ===========================
c handles the keypress 'M'
c --------------------------- 
+ 'M' > 'മ്മ്'

group(lower_n) using keys
c ===========================
c handles the keypress 'm'
c --------------------------- 
store(na_bases) 'കഗഘതസഥധനപഫമശസഹഴ'

+ 'n' > 'ൻ'

'ങ്' + 'n' > context 'ന്'
'ഞ്' + 'n' > context 'ഞ്'
'ഞ്ഞ്' + 'n' > context ''
'ഷ്' + 'n' > context 'ണ്'
'ം' + 'n' > 'മ്ന്'
'ൻ' + 'n' > 'ന്ന്'
'ന്ന്' + 'n' > 'ൻന്ന്'
'മ്ന്' + 'n' > 'ംന്ന്'
'റ്' + 'n' > 'ഋൻ'
'റ്റ്' + 'n' > 'റ്റ്ൻ'
'ങ്ങ്' + 'n' > context dk(n)

any(na_bases) '്' any(mozhi_breakers) dk(mozhi_breakers) + 'n' > index(na_bases,1) '്ൻ'
'ൻ' any(mozhi_breakers) dk(mozhi_breakers) + 'n' > 'ൻൻ'

group(upper_n) using keys
c ===========================
c handles the keypress 'M'
c --------------------------- 
store(kasha) 'കഷ'

+ 'N' > 'ൺ'

'ൺ' + 'N' > 'ണ്ണ്'
'ർ' + 'N' > 'ഋൺ' c 422
'റ്' + 'N' > 'ഋൺ' c 423
'റ്റ്' + 'N' > 'ൺ'

any(kasha) '്' + 'N' > context 'ണ്'
any(mala) 'ർ' + 'N' > context 'ൺ'

group(lower_o) using keys
c ===========================
c handles the keypress 'o'
c --------------------------- 
store(mala_sans_o)  'ഀഁംഃഄ'\
             'അആഇഈഉഊഋഌ഍എഏഐ഑ഒഔ'\
             'കഖഗഘങ'\
             'ചഛജഝഞ'\
             'ടഠഡഢണ'\
             'തഥദധനഩ'\
             'പഫബഭമ'\
             'യരറലളഴവ'\
             'ശഷസഹഺ'\
             '഻഼ഽ'\
             'ാിീുൂൃൄ൅െേൈ൉ൊോൌ്ൎ'\
             '൏'\
             '൐൑൒൓'\
             'ൔൕൖ'\
             'ൗ'\
             '൘൙൚൛൜൝൞ൟൠൡൢൣ'\
             '൤൥'\
             '൦൧൨൩൪൫൬൭൮൯൰൱൲൳൴൵൶൷൸'\
             '൹'\
             'ൺൻർൽൾൿ'

+ 'o' > 'ഒ'

'ഊ' + 'o' > context 'ൗ'
'ള്ൾ' + 'o' > 'ള്' U+200C 'ളൊ'
'ൂ' + 'o' > 'ൂൂ'
'ൗ' + 'o' > 'ൗൗ'
'്ർ' + 'o' > '്' U+200C 'രൊ'
'്' dk(h) + 'o' > 'ൊ'

'ം' + 'o' > 'മൊ'
'അ' + 'o' > 'ഓ'
'ഋ' + 'o' > 'റൊ'
'ഒ' + 'o' > 'ഊ'
'ൻറ്റ്' + 'o' > 'ൻെററാ'
'ൻ്രറ്യ്' + 'o' > 'ൻ്രെറ്യാ'
'റ്രറ്യ്' + 'o' > 'റ്രെറ്യാ'
'ൃ' + 'o' > '്രൊ'
'ൊ' + 'o' > 'ൂ'
'്' + 'o' > 'ൊ'

'ൻറ്' + 'o' > 'ൻെറാ'
'ററ്' + 'o' > 'റെറാ'


any(ka2ta) '്' + 'o' > index(ka2ta, 1) 'ൊ'
any(chillu) + 'o' > index(chillu_bases, 1) 'ൊ' 
any(la_conjuncts) 'ൽ' + 'o' > index(la_conjuncts,1) U+200C 'ലൊ'
any(nna_conjuncts) 'ൺ' + 'o' >  index(la_conjuncts,1) U+200C 'ണൊ'
any(na_conjuncts) 'ൻ' + 'o' >  index(la_conjuncts,1) U+200C 'നൊ'
any(ma_conjuncts) 'ം' + 'o' >  index(la_conjuncts,1) U+200C 'മൊ'

any(mala_sans_o) any(mozhi_breakers) dk(mozhi_breakers) + 'o' > index(mala_sans_o, 1) 'ഒ'
group(upper_o) using keys
c ===========================
c handles the keypress 'O'
c --------------------------- 

+ 'O' > 'ഓ'

'ഓ' + 'O' > context 'ാ'
'ള്ൾ' + 'O' > 'ള്' U+200C 'ളോ'
'ോ' + 'O' > context 'ോ'
'ച്' dk(h) + 'O' > 'ചോ'

'്ർ' + 'O' > '്' U+200C 'രോ'

'ം' + 'O' > 'മോ'
'അ' + 'O' > 'ഓ'
'ഋ' + 'O' > 'റോ'
'ഒ' + 'O' > 'ഊ'
'ൻറ്റ്' + 'O' > 'ൻേററാ'
'ൻ്രറ്യ്' + 'O' > 'ൻ്രേറ്യാ'
'റ്രറ്യ്' + 'O' > 'റ്രേറ്യാ'
'ൻ്രററ്യ്' + 'O' > 'ൻ്രേററ്യാ'
'ൻററ്' + 'O' > 'ൻേററാ'
'ൻ്രറ്' + 'O' > 'ൻ്രേറാ'
'ൻ്രററ്' + 'O' > 'ൻ്രേററാ'
'ൻ്രറ്യ്' + 'O' > 'ൻ്രേറ്യാ'
'റ്രറ്യ്' + 'O' > 'റ്രേറ്യാ'
'ൃ' + 'O' > '്രോ'
'ൃ' dk(r)  + 'O' > '്' U+200C 'രോ'
'ൊ' + 'O' > context 'ോ'
'്' + 'O' > 'ോ'

'ൻറ്' + 'O' > 'ൻേറാ'
'ററ്' + 'O' > 'റേറാ'

any(ka2ta) '്' + 'O' > index(ka2ta, 1) 'ോ'
any(chillu) + 'O' > index(chillu_bases, 1) 'ോ' 
any(la_conjuncts) '്' 'ൽ' + 'O' > index(la_conjuncts, 1) '്' U+200C 'ലോ'
any(nna_conjuncts) '്' 'ൺ' + 'O' > index(nna_conjuncts, 1) '്' U+200C 'ണോ'
any(na_conjuncts) '്' 'ൻ' + 'O' > index(na_conjuncts, 1) '്' U+200C 'നോ'
any(ma_conjuncts) '്' 'ം' + 'O' > index(ma_conjuncts, 1) '്' U+200C 'മോ'


group(lower_p) using keys
c ===========================
c handles the keypress 'p'
c --------------------------- 
store(lm) 'ൽം'

+ 'p' > 'പ്'

'ം' + 'p' > 'മ്പ്'
'ണ്പ്പ്' + 'p' > 'ൺപ്പ്'
'ന്പപ്' + 'p' > 'ൻപ്പ്'
'മ്പ്' + 'p' > 'ംപ്പ്'
'ൽ' + 'p' > 'ല്പ്'
'ല്പ്' + 'p' > 'ൽപ്പ്'
any(lm) any(mozhi_breakers) dk(mozhi_breakers) + 'p' > index(lm, 1) 'പ്'
group(upper_p) using keys
c ===========================
c handles the keypress 'P'
c --------------------------- 

+ 'P' > 'പ്പ്'

group(lower_q) using keys
c ===========================
c handles the keypress 'q'
c --------------------------- 
+ 'q' > 'ഖ്'

group(upper_q) using keys
c ===========================
c handles the keypress 'Q'
c --------------------------- 
+ 'Q' > 'ഘ്'

group(lower_r) using keys
c ===========================
c handles the keypress 'r'
c --------------------------- 
store(r) '0'
 + 'r' > 'ർ'

'സം' + 'r' > context 'ർ'
'റ്റ്' + 'r' > context 'ര്'
'്റ്റ്' + 'r' > '്ട്ര്'
'ം' + 'r' > 'മൃ'
'ൻ' + 'r' > 'നൃ'
'ന്ര്' + 'r' > 'ൻറ്'
'ൻററ്' + 'r' > 'ൻ്രററ്'
'ൻറ്' + 'r' > 'ൻ്രറ്'
'ൻ്റ്' + 'r' > 'ൻട്ര്'
'ർ' + 'r' > 'റ്'
if(breakers="1") 'ർ' + 'r' > U+200C 'റ്' set(breakers = "0")
'നൃ' + 'r' > 'ൻറ്'
'റ്റ്' + 'r' > 'റ്റ്ര്'
'ററ്' + 'r' > 'റ്രറ്'
'്' + 'r' > 'ൃ'
'്ര്' + 'r' > '്റ്'
'ൃ' + 'r' > '്ർ'
'ച്' dk(h) + 'r' > 'ചൃ'

'ൃ' + 'r' > '്റ്' c set(r='1')
'യ്' + 'r' > 'യൃ' dk(r)

any(anu2au) 'ർ' + 'r' > index(anu2au,1) 'റ്'

'സ' any(mozhi_breakers) dk(mozhi_breakers) 'ം' + 'r' > 'സമൃ'
'്' any(mozhi_breakers) dk(mozhi_breakers) + 'r' > '്ർ'  
'റ്' any(mozhi_breakers) dk(mozhi_breakers) + 'r' > 'റ്ർ' set(breakers = "1")  
'ർ' any(mozhi_breakers) dk(mozhi_breakers) + 'r' > 'ർർ' c set(breakers = "1")
group(upper_r) using keys
c ===========================
c handles the keypress 'R'
c --------------------------- 
+ 'R' > 'ഋ'

'ൃ' + 'R' > 'ൃർ'
'ം' + 'R' > 'മൃ'
'ൺ' + 'R' > 'ണൃ'
'ൻ' + 'R' > 'നൃ'
'ർ' + 'R' > 'രൃ'
'ൽ' + 'R' > 'ലൃ'
'ൾ' + 'R' > 'ളൃ'
'്' + 'R' > 'ൃ'
'ച്' dk(h) + 'R' > 'ചൃ'

any(ka2ta) '്' + 'R' > index(ka2ta, 1) 'ൃ'
any(mala) + 'R' > context 'റ്'
group(lower_s) using keys
c ===========================
c handles the keypress 's'
c --------------------------- 
+ 's' > 'സ്'

any(mala) 'ർ' + 's' > index(mala, 1) 'ർസ്'
'ർ' + 's' > 'ഋസ്'
'റ്' + 's' > 'ഋസ്'
'റ്റ്' + 's' > 'റ്റ്സ്'

'ക്' any(mozhi_breakers) dk(mozhi_breakers) + 's' > 'ക്' U+200C 'സ്'

group(upper_s) using keys
c ===========================
c handles the keypress 'S'
c --------------------------- 
+ 'S' > 'ശ്'

group(lower_t) using keys
c ===========================
c handles the keypress 't'
c --------------------------- 
store(nN) 'ൻൺ'

+ 't' > 'റ്റ്'
'ണ്' + 't' > 'ണ്ട്'
'ഷ്' + 't' > 'ഷ്ട്'
'ങ്ങ്' + 't' > 'ങ്റ്റ്'
'ത്' + 't' > 'ത്ത്'
'ട്' + 't' > 'ട്ട്'
'ൺ' + 't' > 'ണ്ട്'
'ണ്ട്' + 't' > 'ണ്ഡ്'
'ദ്' + 't' > 'ഡ്'
'ൻ' + 't' > 'ൻ്റ്'
'ന്ദ്' + 't' > 'ൻഡ്'
'ൻ്റ്' + 't' > 'ണ്ട്'
'ർ' + 't' > 'ഋറ്റ്'
'റ്' + 't' > 'ഋറ്റ്'
'റ്റ്' + 't' > 'ട്'
'ട്ട്' + 't' > context

any(mala) 'ർ' + 't' > index(mala, 1) 'ർട്'
any(nN) '്' + 't' > index(nN, 1) '്ട്'

any(nN) any(mozhi_breakers) dk(mozhi_breakers) + 't' > index(nN, 1) 'റ്റ്'
'്'  any(mozhi_breakers) dk(mozhi_breakers) + 't' > '്' U+200C 'റ്റ്'

group(upper_t) using keys
c ===========================
c handles the keypress 'T'
c --------------------------- 
+ 'T' > 'ട്'
'ൺ' + 'T' > 'ണ്ട്'
'ണ്ട്' + 'T' > 'ൺട്ട്'
'റ്റ്' + 'T' > 'ട്ട്'
any(mozhi_breakers) dk(mozhi_breakers) + 'T' > 'ട്'

group(lower_u) using keys
c ===========================
c handles the keypress 'u'
c --------------------------- 
+ 'u' > 'ഉ'

'ഊ' + 'u' > 'ഊൗ'
'ഔ' + 'u' > 'ഔൗ'
'ള്‌ൾ' + 'u' > 'ള്' u+200C 'ളു'
'ൂ' + 'u' > 'ൂൂ'
'ൗ' + 'u' > 'ൗൗ'
'ൌ' + 'u' > 'ൌൗ'
'്ർ' + 'u' > '്' u+200C 'രു'
'ം' + 'u' > 'മു'
'അ' + 'u' > 'ഔ'
'ഉ' + 'u' > 'ഊ'
'ഋ' + 'u' > 'റു'
'ഒ' + 'u' > 'ഔ'
'ൺ' + 'u' > 'ണു'
'ൻ' + 'u' > 'നു'
'ർ' + 'u' > 'രു'
'ൽ' + 'u' > 'ലു'
'ൾ' + 'u' > 'ളു'
'ു' + 'u' > 'ൂ'
'ൃ' + 'u' > '്രു'
'ൊ' + 'u' > 'ൗ'
'്' + 'u' > 'ു'
'ച്' dk(h) + 'u' > 'ചു'


any(ka2ta) + 'u' > context 'ൗ'
any(la_conjuncts) '്ൽ' + 'u' > index(la_conjuncts, 1) '്' U+200C 'ലു'
any(nna_conjuncts) '്ൺ' + 'u' > index(la_conjuncts, 1) '്' U+200C 'ണു'
any(na_conjuncts) '്ൻ' + 'u' > index(la_conjuncts, 1) '്' U+200C 'നു'
any(ma_conjuncts) '്ം' + 'u' > index(la_conjuncts, 1) '്' U+200C 'മു'



group(upper_u) using keys
c ===========================
c handles the keypress 'U'
c --------------------------- 
+ 'U' > 'ഊ'

'ഊ' + 'U' > 'ഊൗ'
'ൂ' + 'U' > 'ൂൂ'
'ം' + 'U' > 'മൂ'
'ഋ' + 'U' > 'റൂ'
'ൺ' + 'U' > 'ണൂ'
'ൻ' + 'U' > 'നൂ'
'ർ' + 'U' > 'രൂ'
'ൽ' + 'U' > 'ലൂ'
'ൾ' + 'U' > 'ളൂ'
'ൃ' + 'U' > '്രൂ'
'ൃ' dk(r)  + 'U' > '്' U+200C 'രൂ'
'്' + 'U' > 'ൂ'
'ച്' dk(h) + 'U' > 'ചൂ'
if(r='1') 'ർ' + 'U' > '്റൂ' set(r='0')

group(lower_v) using keys
c ===========================
c handles the keypress 'v'
c --------------------------- 
+ 'v' > 'വ്'

'വിൽ' + 'v' > 'വില്വ്'

'ണ്വ്' + 'v' > 'ൺവ്വ്'
'ന്വ്' + 'v' > 'ൻവ്വ്'
'മ്വ്' + 'v' > 'ംവ്വ്'
'ര്വ്' + 'v' > 'ർവ്വ്'
'ല്വ്' + 'v' > 'ൽവ്വ്'
'ള്വ്' + 'v' > 'ൾവ്വ്'

any(chillu) + 'v' > index(chillu_bases, 1) '്വ്'

group(upper_v) using keys
c ===========================
c handles the keypress 'V'
c --------------------------- 
+ 'V' > 'വ്വ്'

group(lower_w) using keys
c ===========================
c handles the keypress 'w'
c --------------------------- 
+ 'w' > 'വ്'

group(upper_w) using keys
c ==========================
c handles the keypress 'W'
c --------------------------- 
+ 'W' > 'വ്വ്'

group(lower_x) using keys
c ===========================
c handles the keypress 'x'
c --------------------------- 
+ 'x' > 'ക്സ്'

group(upper_x) using keys
c ===========================
c handles the keypress 'X'
c --------------------------- 
+ 'X' > 'ക്ഷ്'

group(lower_y) using keys
c ===========================
c handles the keypress 'y'
c --------------------------- 
+ 'y' > 'യ്'

'സം' + 'y' > 'സംയ്'
'റ്രറ്' + 'y' > 'റ്രറ്യ്'
'ം' + 'y' > 'മ്യ്'
'മ്യ്' + 'y' > 'ംയ്യ്'

any(chillu) + 'y' > index(chillu_bases, 1) '്യ്'
any(chillu_bases) '്യ്' + 'y' > index(chillu, 1) 'യ്യ്'
'സ' any(mozhi_breakers) dk(mozhi_breakers) 'ം' + 'y' > 'സമ്യ്'
group(upper_y) using keys
c ===========================
c handles the keypress 'Y'
c --------------------------- 
+ 'Y' > 'യ്യ്'

group(lower_z) using keys
c ===========================
c handles the keypress 'z'
c --------------------------- 
+ 'z' > 'ശ്'

group(upper_z) using keys
c ===========================
c handles the keypress 'Z'
c --------------------------- 
+ 'Z' > 'ഴ്'

group(caret) using keys
c ===========================
c handles the keypress '^'
c --------------------------- 

'ന്റ' + '^' > 'ന്' U+200D 'റ'
'ന്റ്' + '^' > 'ന്' U+200D 'റ'
'ൻ്റ' + '^' > 'ന്‌റ'
'ൻ്റ്' + '^' > 'ന്‌റ്'
any(chillu) + '^' > index(chillu_bases, 1) '്' U+200D

group(hash) using keys
c ===========================
c handles the keypress '#'
c --------------------------- 
store(two2nine) '൨൩൪൫൬൭൮൯'

'ം' + '#' > 'ൔ'
'ൔ' + '#' > 'ഀ'
'ഀ' + '#' > 'ഁ'
'ഁ' + '#' > 'ഄ' c ‎0D04 MALAYALAM LETTER VEDIC ANUSVARA
'ഄ' + '#' > 'ം'

'₹' + '#' > '₨'

'൧൦' + '#' > '൰'
'൧൦൦' + '#' > '൱'
'൧൦൦൦' + '#' > '൲'
'അ' + '#' > 'ഽ'
'ഈ' + '#' > 'ൟ'
'ഋ' + '#' > 'ൠ'
'ക്' + '#' > 'ൿ'
'ന' + '#' > 'ഩ'
'നു' + '#' > '൹'
'ന്' + '#' > 'ഩ്'
'ൻ' + '#' > 'ഩ്'

'ൻറ' + '#' > 'ൻററ'
'ൻറ്' + '#' > 'ൻററ്'
'ൻററൗ' + '#' > 'ൻെററൗ'
'ൻറൗ' + '#' > 'ൻെറൗ'
'ൻറ്' + '#' > 'ൻററ്'
'ൻ്രററൗ' + '#' > 'ൻ്രെററൗ'
'ൻ്രററ്യൗ' + '#' > 'ൻ്രെററ്യൗ'
'ൻ്രറൗ' + '#' > 'ൻ്രെറൗ'
'ൻ്രറ്യൗ' + '#' > 'ൻ്രെറ്യൗ'
'ൻ്റ' + '#' > 'ൻറ'
'ൻ്റ്' + '#' > 'ൻറ്'
'പറ' + '#' > '൏'
'യ്' + '#' > 'ൕ'
'യ്യ' + '#' > 'യ‍്യ'
'യ്യ്' + '#' > 'യ‍്യ്'
'ർ' + '#' > 'ൎ'
'ൽ' + '#' > 'ഌ'
'ഴ്' + '#' > 'ൖ'
'ററ' + '#' > 'ഺ'
'ററൗ' + '#' > 'റെറൗ'
'ററ്' + '#' > 'ഺ്'
'റ്' + '#' > 'ൠ'
'റ്രറൗ' + '#' > 'റ്രെറൗ'
'റ്രറ്യൗ' + '#' > 'റ്രെറ്യൗ'
'റ്റ' + '#' > 'ററ'
'റ്റ്' + '#' > 'ററ്'
'ൃ' + '#' > 'ൄ'
'ൗ' + '#' > 'ൌ'
'്' + '#' > '഼'
'഼' + '#' > '഻'
'്ല്' + '#' > 'ൢ'

'1/10' + '#' > '൜'
'1/16' + '#' > '൶'
'1/160' + '#' > '൘'
'1/2' + '#' > '൴'
'1/20' + '#' > '൛'
'1/320' + '#' > 'പ്ത'
'1/4' + '#' > '൳'
'1/40' + '#' > '൙'
'1/5' + '#' > '൞'
'1/8' + '#' > '൷'
'1/80' + '#' > 'മ'
'10' + '#' > '൰'
'100' + '#' > '൱'
'1000' + '#' > '൲'
'3/16' + '#' > '൸'
'3/20' + '#' > '൝'
'3/4' + '#' > '൵'
'3/80' + '#' > '൚'

any(digits) + '#' > index(mala_digits, 1)
any(two2nine) '൦' + '#' > index(two2nine, 1) '൰'
any(two2nine) '൦൦' + '#' > index(two2nine, 1)  '൱'
any(two2nine) '൦൦൦' + '#' > index(two2nine, 1)  '൲'


any(mozhi_breakers) dk(mozhi_breakers) + '#' > '#'

group(virama) using keys
store(vir) '0'
+ any(virama_keys) > '്' set(vir='1')
any(chillu) any(mozhi_breakers) dk(mozhi_breakers) + any(virama_keys) > index(chillu, 1) '്'
any(chillu) + any(virama_keys) > index(chillu_bases, 1) '്'
'ം' + any(virama_keys) > 'മ്' set(vir='1')
'്' + any(virama_keys) > '്' set(vir='1')

group(back_slash) using keys
+ '\' > set(isEng = '1')
if(isEng='1') + '\' > set(isEng = '0') '\'
